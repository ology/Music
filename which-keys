#!/usr/bin/env perl

# Brute force your way to knowing that a chord belongs to what keys?

use strict;
use warnings;

use Set::Tiny ();
use Data::Dumper::Compact qw(ddc);
use MIDI::Util qw(midi_format);
use Music::Chord::Note ();
use Music::Note ();
use Music::Scales qw(get_scale_notes);

my $chord = shift || 'C'; # Chord names as known to Music::Chord::Note

my $c = Music::Chord::Note->new;

# make sure the notes are flat not sharp, and the doubles are converted to their "true names"
my @notes = midi_format(flatten($c->chord($chord)));
warn 'Chord notes: ', ddc \@notes;
my $notes_set = Set::Tiny->new(@notes); # save the chord notes as a set

# get all the notes, as flats
my @chromatic = get_scale_notes('C', 'chromatic', 0, 'b');

for my $scale_note (@chromatic) {
    is_subset_of($notes_set, $scale_note, 'major');
    is_subset_of($notes_set, $scale_note, 'minor');
}

sub is_subset_of {
    my ($notes_set, $scale_note, $scale_name) = @_;
    # make sure doubles are converted to their "true names"
    my @scale = midi_format(get_scale_notes($scale_note, $scale_name));
    my $scale_set = Set::Tiny->new(@scale);
    print "\tsubset of the $scale_note $scale_name scale\n"
        if $notes_set->is_subset($scale_set);
}

sub flatten {
    my @notes = @_;
    for (@notes) {
        next unless /#/;
        my $note = Music::Note->new($_, 'isobase');
        $note->en_eq('flat');
        $_ = $note->format('isobase');
    }
    return @notes;
}
