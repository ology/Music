#!/usr/bin/env perl
use strict;
use warnings;

use Data::Dumper::Compact 'ddc';
use List::Util qw(shuffle);
use lib map { "$ENV{HOME}/sandbox/$_/lib" } qw(MIDI-Praxis-Variation MIDI-Util Music-Interval-Barycentric);
use MIDI::Praxis::Variation qw(transposition);
use MIDI::Util;
use Music::Interval::Barycentric;
use Music::Scales;
use Music::VoiceGen;

my $bars   = shift || 32;
my $bpm    = shift || 180;
my $note   = shift || 'B';
my $scale  = shift || 'major';

# Create a voice generator
my $octave = 2;
my @pitches = (
    get_scale_MIDI($note, $octave, $scale),
    get_scale_MIDI($note, $octave + 1, $scale),
    get_scale_MIDI($note, $octave + 2, $scale),
);
my $voice = Music::VoiceGen->new(
    pitches   => \@pitches,
    intervals => [qw(-4 -3 -2 -1 1 2 3 4)],
);

# Clarinet track globals
my $cmotif = [shuffle qw(dhn en en en en)];
my @cnotes;

# Generate notes for the piano tracks
my $pmotif = [('en') x 10];
my @pnotes;
my @transp;
for my $n (0 .. $#$pmotif) {
    my $note = note_or_rest($n, $pmotif, \@pnotes);
    push @pnotes, $note;
    if ($note eq 'r') {
        push @transp, 'r';
    }
    else {
        my @transposed = transposition(-12, $note);
        push @transp, $transposed[0];
    }
}

# Prepare the MIDI
my $volume = 98;
my $pan = 10; # control change #
my $pan_left = 32;
my $pan_right = 86;
my $score = MIDI::Util::setup_score(
    lead_in   => 0,
    signature => '5/4',
    bpm       => $bpm,
    volume    => $volume,
);

# Add each part to the score in parallel
$score->synch(
    \&piano1,
    \&piano2,
    \&violin1,
    \&violin2,
    \&clarinet1,
    \&clarinet2,
);

# Output the score as a MIDI file
$score->write_score("$0.mid");

########################################

# Play the pre-computed piano notes
sub piano1 {
    MIDI::Util::set_chan_patch($score, 0, 0);
    $score->control_change(0, $pan, $pan_left);
    print 'Piano 1.1: ', ddc(\@pnotes);
    print 'Piano 1.2: ', ddc(\@transp);
    for my $i (1 .. $bars) {
        for my $n (0 .. $#$pmotif) {
            play_note_or_rest($pmotif->[$n], [$pnotes[$n], $transp[$n]]);
        }
    }
}

# Play a random cyclic permutation of the transposed piano
sub piano2 {
    MIDI::Util::set_chan_patch($score, 1, 0);
    $score->control_change(1, $pan, $pan_right);
    my @rotated = cyclic_permutation(@transp);
    shift @rotated; # Don't use the same permutation as piano 1
    my $rotated = $rotated[ int rand @rotated ];
    print 'Piano 2: ', ddc($rotated);
    for my $i (1 .. $bars) {
        for my $n (0 .. $#$pmotif) {
            play_note_or_rest($pmotif->[$n], [$rotated->[$n]]);
        }
    }
}

# Alternate bars between 2 sets of notes
sub violin1 {
    MIDI::Util::set_chan_patch($score, 2, 40);
    $score->control_change(2, $pan, $pan_left);
    $score->Volume(70); # XXX The violin patch is too loud!
    my $vmotif = [shuffle qw(dhn hn)];
    print 'Violin 1 durations: ', ddc($vmotif);
    my @vnotes1;
    my @vnotes2;
    for my $n (0 .. $#$vmotif) {
        my $note = note_or_rest($n, $vmotif, \@vnotes1);
        push @vnotes1, $note;
        $note = note_or_rest($n, $vmotif, \@vnotes2);
        push @vnotes2, $note;
    }
    print 'Violin 1.1: ', ddc(\@vnotes1);
    print 'Violin 1.2: ', ddc(\@vnotes2);
    for my $i (1 .. $bars) {
        for my $n (0 .. $#$vmotif) {
            if ($i % 2) {
                play_note_or_rest($vmotif->[$n], [$vnotes1[$n]]);
            }
            else {
                play_note_or_rest($vmotif->[$n], [$vnotes2[$n]]);
            }
        }
    }
    $score->Volume($volume);
}

# Alternate bars with rests or notes
sub violin2 {
    MIDI::Util::set_chan_patch($score, 3, 40);
    $score->control_change(3, $pan, $pan_right);
    $score->Volume(70); # XXX The violin patch is too loud!
    my $vmotif = [shuffle qw(dhn hn)];
    print 'Violin 2 durations: ', ddc($vmotif);
    my @vnotes;
    for my $n (0 .. $#$vmotif) {
        my $note = note_or_rest($n, $vmotif, \@vnotes);
        push @vnotes, $note;
    }
    print 'Violin 2: ', ddc(\@vnotes);
    for (1 .. 7) {
        $score->r('dhn');
        $score->r('hn');
    }
    for my $i (1 .. $bars - 7) {
        for my $n (0 .. $#$vmotif) {
            if ($i % 2) {
                $score->r($vmotif->[$n]);
            }
            else {
                play_note_or_rest($vmotif->[$n], [$vnotes[$n]]);
            }
        }
    }
    $score->Volume($volume);
}

# Populate and play the clarinet notes
sub clarinet1 {
    MIDI::Util::set_chan_patch($score, 4, 71);
    $score->control_change(4, $pan, $pan_left);
    for my $n (0 .. $#$cmotif) {
        my $note = note_or_rest($n, $cmotif, \@cnotes);
        push @cnotes, $note;
    }
    print 'Clarinet 1 durations: ', ddc($cmotif);
    print 'Clarinet 1: ', ddc(\@cnotes);
    for (1 .. 23) {
        $score->r('dhn');
        $score->r('hn');
    }
    for my $i (1 .. $bars - 23) {
        for my $n (0 .. $#$cmotif) {
            play_note_or_rest($cmotif->[$n], [$cnotes[$n]]);
        }
    }
}

# Get and play new clarinet notes for the non-rest motif positions
sub clarinet2 {
    MIDI::Util::set_chan_patch($score, 5, 71);
    $score->control_change(5, $pan, $pan_right);
    my @cnotes2;
    for my $n (@cnotes) {
        my $note = $n eq 'r' ? 'r' : $voice->rand;
        push @cnotes2, $note;
    }
    print 'Clarinet 2 durations: ', ddc($cmotif);
    print 'Clarinet 2: ', ddc(\@cnotes2);
    for (1 .. 23) {
        $score->r('dhn');
        $score->r('hn');
    }
    for my $i (1 .. $bars - 23) {
        for my $n (0 .. $#$cmotif) {
            play_note_or_rest($cmotif->[$n], [$cnotes2[$n]]);
        }
    }
}

# Either return a note or a rest!
sub note_or_rest {
    my ($n, $motif, $notes) = @_;
    if (
        # We're at the end of the motif and the first note is a rest
        ($n == $#$motif && $notes->[0] eq 'r')
        ||
        # The previous note is a rest
        (defined $notes->[$n - 1] && $notes->[$n - 1] eq 'r')
    ) {
        $note = $voice->rand;
    }
    else {
        $note = int(rand 10) <= 3 ? 'r' : $voice->rand;
    }
    return $note;
}

# Either play a note or a rest!
sub play_note_or_rest {
    my ($motif, $notes) = @_;
    if ($notes->[0] eq 'r') {
        $score->r($motif);
    }
    else {
        $score->n($motif, @$notes);
    }
}
